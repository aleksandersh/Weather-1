apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'

def keystorePropertiesFile = rootProject.file("keystore.properties")
def apiPropertiesFile = rootProject.file("api.properties")
def keystoreProperties = new Properties()
def apiProperties = new Properties()
apiProperties.load(new FileInputStream(apiPropertiesFile))
if (keystorePropertiesFile.exists())
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
else
    keystoreProperties.storeFile = 'whatever'

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.0"
    signingConfigs {
        config {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }
    defaultConfig {
        applicationId "ru.yamblz.weather"
        minSdkVersion 17
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        signingConfig signingConfigs.config
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    buildTypes.each {
        it.buildConfigField 'String', "BASE_URL", apiProperties['BaseUrl']
        it.buildConfigField 'String', 'API_KEY', apiProperties['DarkSkyKey']
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile "com.android.support:appcompat-v7:$support"
    compile "com.android.support:cardview-v7:$support"
    compile "com.android.support:preference-v7:$support"
    compile "com.android.support:design:$support"
    //ButterKnife
    compile "com.jakewharton:butterknife:$butterKnife"
    annotationProcessor "com.jakewharton:butterknife-compiler:$butterKnife"
    //Dagger 2
    compile "com.google.dagger:dagger:$dagger"
    annotationProcessor "com.google.dagger:dagger-compiler:$dagger"
    //RxJava 2
    compile "io.reactivex.rxjava2:rxandroid:$rxandroid"
    compile "io.reactivex.rxjava2:rxjava:$rxjava"
    //Retrofit 2
    compile "com.squareup.retrofit2:retrofit:$retrofit"
    compile "com.squareup.retrofit2:converter-gson:$retrofit"
    compile "com.squareup.retrofit2:adapter-rxjava2:$retrofit"
    compile "com.squareup.okhttp3:logging-interceptor:$okhttp"
    //Preferences
    compile "com.github.nikolaymanzhos:preferenceshelper:$preferenceshelper"
    //MaterialSettings
    compile "com.yarolegovich:mp:$materialsettings"
    //GCM
    compile "com.google.android.gms:play-services-gcm:$gcm"

    //Icepick
    compile "frankiesardo:icepick:$icepick"
    provided "frankiesardo:icepick-processor:$icepick"
    testCompile "junit:junit:$junit"
    testCompile "org.mockito:mockito-core:$mockito"
    testCompile "org.robolectric:robolectric:$robolectric"
    androidTestCompile("com.android.support.test.espresso:espresso-core:$espresso", {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
}
